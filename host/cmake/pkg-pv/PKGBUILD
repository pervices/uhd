pkgname=libuhdpv
pkgver=4.4.0.0
_pkgid=git
pkgrel=1
nightly=0
pkgdesc="Official Per Vices libuhdpv build script supporting all Per Vices SDR products"
arch=('x86_64')
url="https://www.support.pervices.com"
license=('GPL')
makedepends=('cmake' 'glibc' 'git' 'orc' 'libusb' 'boost' 'boost-libs' 'icu' 'python-mako' 'python-requests' 'lapack' 'python-numpy' 'python-retrying' 'python-scipy' 'doxygen' 'gpsd' 'python-setuptools' 'gtk3')

provides=('libuhdpv')
conflicts=('libuhd')

source=('uhd::git+https://github.com/pervices/uhd.git#branch=master')
#pkgsrc=('file://uhd.tar.gz')
#You can specify #branch=,#commit=,#tag=fragment

md5sums=('SKIP')
sha1sums=('SKIP')

_gitname="uhd"

install=$pkgname.install

pkgver() {
    if [ $nightly == '0' ]; then
        cd ${srcdir}/${_gitname}/host/cmake/debian-pv
        echo $(cat changelog | head -1 | cut -d ' ' -f2 | cut -d '-' -f1 | tr --delete ['('])
    else
        cd ${srcdir}/$_gitname
        echo -n `date +%Y%m%d%H%M`+g && git rev-parse --short HEAD
    fi
}

build() {
    cd ${srcdir}/$_gitname/host
    mkdir -p build
    cd build
    cmake \
       -DCMAKE_INSTALL_PREFIX=/usr/ \
       -DPYTHON_EXECUTABLE=/usr/bin/python3 \
       -DENABLE_EXAMPLES=ON \
       -DENABLE_UTILS=ON \
       -DENABLE_DPDK=OFF \
       -DENABLE_TESTS=OFF \
       -DENABLE_N300=OFF \
       -DENABLE_E320=OFF \
       -DENABLE_USRP1=OFF \
       -DENABLE_USRP2=ON \
       -DENABLE_B200=OFF \
       -DENABLE_B100=OFF \
       -DENABLE_X300=OFF \
       -DENABLE_CRIMSON_TNG=ON \
       -DENABLE_OCTOCLOCK=OFF \
       -DENABLE_DOXYGEN=OFF \
       -DENABLE_USB=OFF \
       -DENABLE_LIBURING=OFF \
       -DENABLE_PYTHON_API=ON \
       ..
    make -j$(nproc)
}

check() {
    cd "$srcdir/$_gitname/host/build"
    make test
}

package() {
    depends=('python3' 'boost-libs' 'glibc' 'gcc-libs' 'libusb')
    cd "$srcdir/$_gitname/host/build"
    make DESTDIR="$pkgdir" install
    install -Dm644 "../utils/uhd-usrp.rules" "$pkgdir/usr/lib/udev/rules.d/10-uhd-usrp.rules"
}
